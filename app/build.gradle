import java.text.SimpleDateFormat

plugins {
    id 'com.android.application'
    id 'org.greenrobot.greendao'
}

android {
    namespace 'com.hf.newmidx'
    compileSdk 34

    buildFeatures {
        viewBinding = true
    }

    defaultConfig {
        applicationId "com.hf.newmidx"
        minSdk 24
        targetSdk 34
        versionCode 2
        versionName "v1.0.1"
        ndk {
            abiFilters  "arm64-v8a"
        }
    }

    // 自定义APK文件名
    android.applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "newmidx_${versionName}_${releaseTime()}.apk"
        }
    }

    greendao {
        schemaVersion 1
        daoPackage 'com.hf.newmidx.greendao.gen'
        targetGenDir 'src/main/java'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main {
            jniLibs.srcDir 'libs'
        }
    }
}

//获取时间
static def releaseTime() {
    return new SimpleDateFormat("yyyy-MM-dd-HH-mm", Locale.CHINA).format(new Date())
}


dependencies {
    api fileTree(include: ['*.aar','*.jar'], dir: 'libs')
    implementation project(path: ':libscan')
    implementation project(path: ':lib-eyecooliris-uvc')
    implementation 'androidx.appcompat:appcompat:1.7.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.2.1'
    //permission
    implementation 'com.github.getActivity:XXPermissions:20.0'
    //db
    implementation 'org.greenrobot:greendao:3.3.0'
    implementation 'io.github.yuweiguocn:GreenDaoUpgradeHelper:v2.2.1'

    implementation ('com.github.li-xiaojun:XPopup:2.10.0')
}